// **********************************************************************
//
// Copyright (c) 2003-2018 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

//
// Instal gradle Ice builder plug-in from the plug-in portal
//
plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.4.5' apply false
}

subprojects {
    //
    // Apply Java and Slice plug-ins to all subprojects
    //
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'

    //
    // Both projects Client and Server share the Printer.ice Slice definitions
    //
    slice {
        java {
            files = [file("../Printer.ice")]
        }
    }

    //
    // Use Ice JAR files from maven central repository, fallback to ZeroC testing
    // repository (Useful for release testing)
    //
    repositories {
        mavenCentral()
        maven {
            url "https://builds.testing.zeroc.com/nexus/repository/maven.ice-3.7"
        }
    }

    //
    // Both Client and Server depend only on Ice JAR
    //
    dependencies {
        compile 'com.zeroc:ice:3.7.1'
    }

    //
    // Create a JAR file with the appropiate Main-Class and Class-Path attributes
    //
    jar {
        manifest {
            attributes(
                "Main-Class" : project.name.capitalize(),
                "Class-Path": configurations.runtime.resolve().collect { it.toURI() }.join(' ')
            )
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.7"
}
