// **********************************************************************
//
// Copyright (c) 2003-2017 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

def demoIceGridProject(name) {
    project(name) {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            compile localDependency("ice")
            compile localDependency("icessl")
            compile localDependency("icegrid")
            compile localDependency("icelocatordiscovery")
            if (!useLocalOnly) {
                runtime "org.apache.commons:commons-compress:1.14"
            }
        }

        task makeDbDirs << {
            file('db/registry').mkdirs()
            file('db/node').mkdirs()
        }
        compileJava.dependsOn(makeDbDirs)

        clean {
            delete 'db'
        }

        // Don't create the default jar.
        jar.enabled = false
    }
}

demoIceGridProject(":demoIceGridSimple")
demoIceGridProject(":demoIceGridIcebox")

project(":demoIceGridSimple") {
    demoJar("client")
    demoJar("server")
}

project(":demoIceGridIcebox") {
    dependencies {
        compile localDependency("icessl")
        compile localDependency("icebox")
    }
    demoJar("client")
    demoJar("service")
    demoIceBoxServer()
}
