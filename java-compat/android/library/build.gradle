// **********************************************************************
//
// Copyright (c) 2003-2018 ZeroC, Inc. All rights reserved.
//
// **********************************************************************

apply plugin: 'com.android.application'
apply from: "$rootProject.projectDir/gradle/ice.gradle"

project.slice.output = project.file("${project.buildDir}/generated/source/ice")

slice {
    java {
        set1 {
            include = ["${projectDir}/src/main/slice"]
            srcDir = 'src/main'
        }
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.zeroc.library"
        minSdkVersion 21
        targetSdkVersion 21
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

task copyBksTask(type: Copy) {
    from "${rootProject.projectDir}/../../certs/client.bks"
    into "src/main/res/raw"
}
preBuild.dependsOn(copyBksTask)

clean {
    delete("src/main/res/raw/client.bks")
}

dependencies {
    implementation 'com.zeroc:ice-compat:3.7.1'
    implementation 'com.zeroc:glacier2-compat:3.7.1'
}
