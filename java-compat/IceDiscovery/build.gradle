//
// Copyright (c) ZeroC, Inc. All rights reserved.
//

['hello', 'replication'].each {
    def name = it
    project(":demoIceDiscovery${it.capitalize()}") {
        slice {
            java {
                include = ["${projectDir}"]
                srcDir = '.'
            }
        }

        sourceSets {
            main {
                java {
                    srcDir '.'
                }
            }
        }

        dependencies {
            implementation localDependency("ice-compat")
            runtimeOnly localDependency("icediscovery-compat")
            runtimeOnly "org.apache.commons:commons-compress:1.14"
        }

        // Don't create the default jar.
        jar.enabled = false

        demoJar("client")
        demoJar("server")
    }
}
